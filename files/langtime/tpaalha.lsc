# The * feature value is the default value
Feature Type(*cons, vowel)

# Vowels
Feature Height(low, mid, high)
Feature Depth(front, central, back)
Feature Stress(*unstressed, secondary, stressed)
Feature Length(*short, long)
Feature Diphthong(*monophthong, diphthong)

# Consonants
Feature Voicing(unvoiced, voiced)
Feature Place(bilabial, pallabial, alveolar, retroflex, palatal, velar, glottal)
Feature Manner(stop, affricate, fricative, nasal, liquid)
Feature Lateral(*nonlateral, lateral)

# "Floating" diacritics are ignored in rules by default
Diacritic ˈ (floating) [stressed]
Diacritic ː [long]
Diacritic ʲ [pallabial]

Symbol ɛ [vowel front mid]
Symbol i [vowel front high]
# Seems easiest if we treat the diphthongs as single sounds
Symbol ie [vowel front high diphthong]
Symbol a [vowel central low]
Symbol ə [vowel central mid]
Symbol ɨ [vowel central high]
Symbol ʌ [vowel back mid]
Symbol ʌɯ [vowel back mid diphthong]
Symbol ɯ [vowel back high]
Symbol ɯʌ [vowel back high diphthong]

Symbol p [unvoiced bilabial stop]
Symbol b [voiced bilabial stop]
Symbol ʈ [unvoiced retroflex stop]
Symbol ɖ [voiced retroflex stop]
Symbol c [unvoiced palatal stop]
Symbol ɟ [voiced palatal stop]
Symbol k [unvoiced velar stop]
Symbol ɡ [voiced velar stop]
# Use the tie to visually distinguish t͡s from the cluster ts
Symbol t͡s [unvoiced alveolar affricate]
Symbol d͡z [voiced alveolar affricate]
Symbol ʈ͡ʂ [unvoiced retroflex affricate]
Symbol ɖ͡ʐ [voiced retroflex affricate]
Symbol ɸ [unvoiced bilabial fricative]
Symbol β [voiced bilabial fricative]
Symbol s [unvoiced alveolar fricative]
Symbol z [voiced alveolar fricative]
Symbol ʂ [unvoiced retroflex fricative]
Symbol ʐ [voiced retroflex fricative]
Symbol ç [unvoiced palatal fricative]
Symbol ʝ [voiced palatal fricative]
Symbol x [unvoiced velar fricative]
Symbol ɣ [voiced velar fricative]
Symbol h [unvoiced glottal fricative]
Symbol m [voiced bilabial nasal]
Symbol n [voiced alveolar nasal]
Symbol ɳ [voiced retroflex nasal]
Symbol ɲ [voiced palatal nasal]
Symbol ŋ [voiced velar nasal]
Symbol w [voiced bilabial liquid]
Symbol ʍ [unvoiced bilabial liquid]
Symbol ɻ [voiced retroflex liquid]
Symbol ɭ [voiced lateral retroflex liquid]
Symbol ɬ [unvoiced lateral retroflex liquid]
Symbol j [voiced palatal liquid]
Symbol ʎ [voiced lateral palatal liquid]

Class unpalatalized {p, b, ʈ, ɖ, k, ɡ, ɸ, s, z, ʂ, ʐ, x, ɻ, ɭ, m, n}
Class palatalized {pʲ, bʲ, c, ɟ, c, ɟ, ç, ç, ʝ, ç, ʝ, ç, j, ʎ, mʲ, ɲ}
Class devoiceable {b, bʲ, ɖ, ɟ, ɡ, β, z, ʐ, ʝ, ɣ}
Class devoiced {p, pʲ, ʈ, c, k, ɸ, s, ʂ, ç, x}
Class sibilant {s, z, ʂ, ʐ}
Class frequickative {ɸ, β, x, ɣ}

Deromanizer:
 ǝˈ => əˈ
 ǝ => ə
 g => ɡ
 u => ɯ
 o => ʌ
 {t, d, r, l} => {ʈ, ɖ, ɻ, ɭ}

stress:
 [vowel] => [stressed] / _ {[cons]* [vowel unstressed], [cons]+} $
 [vowel] => [stressed] / $ [cons]* _ $

weak-migration:
 {ʌ, ɛ} => {ə, ɨ} / _ []* [stressed] // $ _

mid-rise:
 {ɛˈ, ʌˈ} => {iˈ, ɯˈ}

central-vowel-annihilation:
 {əˈ, ɨˈ} => {ʌˈ, ɯˈ}

glomination:
 $$ => *
 Then:
 [stressed] => [unstressed] / [stressed] [unstressed]* _

voiced-lengthening:
 [vowel] => [long] / _ [voiced] $

palatalization:
 @unpalatalized ɨ => @palatalized *
 @unpalatalized => @palatalized / _ {i, ɨ, iː, ɨː}

palatalization-spreading propagate:
 # Assuming that /j/ doesn't trigger palatalization spreading
 @unpalatalized => @palatalized / @palatalized _ // j _

weak-loss:
 {ə, ɨ} => * / [cons] _ [cons]

yeetification:
 j => * / {_ {i, iː}, @palatalized _}
 ʎ => * / _ {i, iː}

nasal-assimilation:
 [nasal] => [$Place] / _ [cons $Place]

voicing-assimilation propagate:
 @devoiceable => @devoiced / @devoiced _
 @devoiced => @devoiceable / @devoiceable _

voicing-assimilation-full-dandy:
 {xɭ, xɻ, ɸɭ, ɸɻ} => {ɬ, xʍ, ɸɬ, ʍ}

retroflex-assimilation propagate:
 {@sibilant, ç, ʝ, ɲ} => [retroflex] / {[retroflex] _, _ [retroflex]}

sibilant-assimilation propagate:
 {@sibilant, ç, ʝ} => $1 / _ {@sibilant, ç, ʝ}$1

fricative-fortition:
 {@sibilant, @frequickative} => {[affricate], [stop]} / [nasal] _

weak-fricative-dissimilation:
 @frequickative => [stop] / _ {@sibilant, ç, ʝ}

vowel-breaking:
 {ʌː, iː, ɯː} => [short diphthong]

final-devoicing-and-loganian-merger:
 {@devoiceable, ɭ, ɻ} => {@devoiced, ɬ, ɬ} / _ $

frequickative-debooklization:
 @frequickative => h / _ $

vowel-polarization:
 # Lexurgy does not recognize the crazy _,# syntax!
 {ɛ, ʌ} => {i, ɯ} / {$ _, _ $}

boundary-weak-loss:
 {ə, ɨ} => * / {$ _, _ $}

epenthetic-i:
 * => i / $ _ {[nasal], [liquid]} [cons]
 Then:
 * => i / $ _ [cons]$1 $1

Romanizer-phonetic literal:
 * => ^ / {$, [vowel]} _ [cons]? [stressed]
 * => ^ / $ _ [cons] [cons]+ [stressed]
 * => ^ / [vowel] [cons] _ [cons]+ [stressed]
 Then:
 [stressed] => [unstressed]
 Then:
 ^ => ˈ

Romanizer:
 [long]$1 * => $1 $1
 Then:
 [long] => [short]
 Then:
 [stressed] => [unstressed] / [stressed] _
 Then:
 {ɛ, ʌ, ɯ, ʌɯ, ɯʌ} => {e, o, u, ou, uo}
 {ɸ, β, ʍ, pʲ, bʲ, ɸʲ, βʲ, mʲ} => {f, v, w, py, by, fy, vy, my}
 {t͡s, d͡z} => {ts, dz}
 {ʈ, ɖ, ʂ, ʐ, ʈ͡ʂ, ɖ͡ʐ, ɭ, ɬ, ɻ, ɳ} => {t, d, sh, zh, ch, j, l, lh, r, n}
 {c, ɟ, ç, ʝ, j, ʎ, ɲ} => {ty, dy, sy, zy, y, ly, ny}
 {x, ɣ, ŋ} => {kh, gh, n}
 Then:
 {aˈ, eˈ, iˈ, oˈ, uˈ, ieˈ, ouˈ, uoˈ} => {á, é, í, ó, ú, ié, oú, uó}
 {shsh, zhzh} => {ssh, zzh}
 yy => y
