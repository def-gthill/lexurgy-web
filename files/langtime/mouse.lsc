Feature +ejective
Feature +long, hightone, +nasalized
Diacritic ʼ [+ejective]
Diacritic ́  (floating) [+hightone]
Diacritic ̀  (floating) [-hightone]
Diacritic ̃  (floating) [+nasalized]
Diacritic ː (floating) [+long]
Symbol ts, tʃ, bv, dz, dʒ
Class glide {w, j, ɥ}
Class nasal {m, n, ɲ, ŋ}
Class sonorant {ɹ, l, @nasal}
Class cons {p, pʼ, bv, t, tʼ, ts, tsʼ, dz, dʒ, c, cʼ, tʃ, tʃʼ, k, kʼ, ʔ, f, fʼ, v, θ, θʼ, ð, s, sʼ, z, ʒ, h, @glide, @sonorant}
Class vowel {a, æ, e, i, ɑ, o, u, y}

deromanizer literal:
 ' => ʼ
 r => ɹ

Syllables:
    {@cons, (@cons // @vowel _)? @glide, (* / $ _)} @vowel {@vowel, @cons&[-ejective]}?

fricative-voicing:
    {s, f} => {z, v} / @sonorant _

glide-fortition:
    w => v / _ {o, u}
    j => ʒ / _ {e, i}

epenthesis:
    {z, ʒ, v} => {dz, dʒ, bv} / @nasal _ @vowel

hiatus-resolution-early:
    {ii, ie, iu, io, ia} => {iː, je, ju, jo, ja}
    {ei, ee, eu, eo, ea} => {ei, ei, eu, eu, æ}
    {ui, ue, uu, uo, ua} => {wi, we, yː, wo, wa}
    {oi, oe, ou, oo, oa} => {oi, oi, ou, ou, ɑ}
    {ai, ae, au, ao, aa} => {ai, ai, au, au, aː}

coda-changes-early:
    {ij, ej, uj, oj, aj} => {ìː, èì, ỳ, ỳ, àì} / _ .
    {iw, ew, uw, ow, aw} => {ỳ, ỳ, ỳː, òù, àù} / _ .
    {iɹ, eɹ, uɹ, oɹ, aɹ} => {jà, jà, wà, wà, àː} / _ .
    f => h / _ .
    @vowel s => [-hightone +long] * / _ .
    @vowel {p, t, ts, k} => [+hightone] ʔ / _ .
    {ic, ec, uc, oc, ac} => {íʔ, íʔ, ýʔ, ýʔ, éʔ} / _ .
    @vowel => [+nasalized] / _ {m, n, ɲ, ŋ} .
    Then:
    {ã, ẽ, ĩ, õ, ũ} => {ẽ, ĩ, ĩ, ỹ, ỹ} / _ ɲ .
    {ĩ, ũ} => {ẽ, õ}
    Then:
    {m, n, ɲ, ŋ} => * / _ .

palatal-simplification:
    j => * / {ʒ, ɲ} _

hiatus-resolution-late:
    {iː, ei, eu, æ, yː, oi, ou, ɑ, ai, au, aː} => {i, i, y, e, y, we, u, o, e, o, a}
    u => y

default-tone:
    @vowel&[*hightone] => [+hightone]

velarization:
    {t, tʼ, ɹ, n} => {k, kʼ, *, ŋ} / _ w

glide-decimation:
    @glide => * / {@glide, @sonorant} _

glide-assimilation:
    w => ɥ / _ y

coda-changes-late:
    @vowel h => [-hightone] * / _ .
    {eː, oː} => {i, u}
    ʔ => *
    [+nasalized] => [-nasalized -hightone]
    # Then:
    # {j, w} => [*hightone]

Syllables:
    @cons? @glide? @vowel

rhotic-lateralization:
    ɹ => l

dento-affricatial-pull-chain:
    {ts, tsʼ, dz, sʼ} => {θ, θʼ, ð, tsʼ}

palatal-fronting:
    {c, cʼ} => {tʃ, tʃʼ}

