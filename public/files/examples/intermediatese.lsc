
Feature type(*cons, vowel)

Feature place(bilabial, alveolar, velar, glottal)
Feature manner(stop, fricative, nasal, lateral)

Feature height(low, mid, high)
Feature frontness(front, central, back)

Feature +long
Feature +stress

Diacritic ː [+long]
Diacritic ˈ [+stress]

Symbol p [bilabial stop]
Symbol t [alveolar stop]
Symbol k [velar stop]
Symbol ʔ [glottal stop]
Symbol s [alveolar fricative]
Symbol h [glottal fricative]
Symbol m [bilabial nasal]
Symbol n [alveolar nasal]
Symbol l [alveolar lateral]

Symbol a [low central vowel]
Symbol e [mid front vowel]
Symbol i [high front vowel]
Symbol o [mid back vowel]
Symbol u [high back vowel]

deromanizer:
 ' => ʔ

stress-first-syllable:
 [vowel] => [+stress] / $ [cons]? _

syncope:
 [-stress vowel] => * / {[!stop] _ [cons], [stop] _ [stop]}

coda-drop-and-lengthen:
 [vowel] {h, [stop]} => [+long] * / _ [cons]

nasal-assimilation:
 [nasal] => [$place] / _ [cons !glottal $place]

romanizer-phonetic:
 unchanged

romanizer:
 # Get rid of the stress marks, since stress is still predictable
 [+stress] => [-stress]
 Then:
 {aː, eː, iː, oː, uː} => {ā, ē, ī, ō, ū}
 ʔ => '
